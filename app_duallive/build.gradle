apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def signature_name = 'duallive.jks'
def storePass = '123456'
def keAlias = 'duallive'
def keyPass = '123456'

def keyStore = file(signature_name)

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.eyecool.face.duallive.demo"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 2
        versionName rootProject.ext.versionName

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(signature_name)
            storePassword project.hasProperty('STOREPASS') ? STOREPASS : storePass
            keyAlias project.hasProperty('KEYALIAS') ? KEYALIAS : keAlias
            keyPassword project.hasProperty('KEYPASS') ? KEYPASS : keyPass
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (keyStore.exists()) {
                println "using eyecool's key"
                signingConfig signingConfigs.release
            } else {
                println "using default key"
            }
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "ec_duallive_${variant.buildType.name}_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'

    implementation project(':lib-eyecoolface-detect')
    implementation project(':lib-eyecoolface-duallive')
    implementation project(':lib-eyecoolface')
}
